Imports System
Imports System.IO
Imports System.Collections.Generic
Imports System.Threading

Module Program
    Sub Main(args As String())
        ' Ensure only one instance can run at a time using a named Mutex
        Dim mutexName As String = "Global\UI_Generator_SingleInstance"
        Dim createdNew As Boolean = False

        Using mutex As New Mutex(True, mutexName, createdNew)
            If Not createdNew Then
                Console.WriteLine("Another instance of UI_Generator is already running.")
                Return
            End If

            ' Use relative path from the executable's location
            Dim appPath As String = AppDomain.CurrentDomain.BaseDirectory
            Dim vbClassesPath As String = Path.GetFullPath(Path.Combine(appPath, "..\..\..\..\..\VBClasses"))
            Dim vbClasses As New SortedList(Of String, String)
            Dim xoClasses As New SortedList(Of String, String)
            Dim totalLinesRead As Integer = 0

            ' Get all .vb files in the VBClasses directory
            Dim vbFiles() As String = Directory.GetFiles(vbClassesPath, "*.vb")

            For Each vbFile As String In vbFiles
                Dim lines() As String = File.ReadAllLines(vbFile)

                For Each line As String In lines
                    Dim trimmedLine As String = line.Trim()

                    ' Count non-empty, non-comment lines
                    If Not String.IsNullOrEmpty(trimmedLine) AndAlso Not trimmedLine.StartsWith("'") Then
                        totalLinesRead += 1
                    End If

                    If line.Contains(": Inherits TaskParent") Then
                        ' Parse the class name from lines like: "Public Class ClassName : Inherits TaskParent"
                        Dim className As String = ExtractClassName(line)
                        If Not String.IsNullOrEmpty(className) Then
                            If className.StartsWith("XO_") Then
                                If Not xoClasses.ContainsKey(className) Then
                                    xoClasses.Add(className, className)
                                End If
                            Else
                                If Not vbClasses.ContainsKey(className) Then
                                    vbClasses.Add(className, className)
                                End If
                            End If
                        End If
                    End If
                Next
            Next

            ' Write the counts to AlgorithmCounts.txt
            Dim dataPath As String = Path.GetFullPath(Path.Combine(appPath, "..\..\..\..\..\Data"))
            Dim countsFilePath As String = Path.Combine(dataPath, "AlgorithmCounts.txt")
            Dim countsContent As String = "CodeLineCount = " & totalLinesRead.ToString() & vbCrLf &
                                          "AlgorithmCount = " & vbClasses.Count.ToString() & vbCrLf &
                                          "Reference algorithm count = " & xoClasses.Count.ToString() & vbCrLf &
                                          "Average lines per algorithm = " &
                                          (totalLinesRead \ (vbClasses.Count + xoClasses.Count)).ToString() & vbCrLf
            File.WriteAllText(countsFilePath, countsContent)

            ' Generate AlgorithmList.vb in the Main directory
            Dim mainPath As String = Path.GetFullPath(Path.Combine(appPath, "..\..\..\..\..\Main"))
            Dim algorithmListPath As String = Path.Combine(mainPath, "AlgorithmList.vb")
            Dim sb As New System.Text.StringBuilder()

            sb.AppendLine("' this file is automatically generated in a pre-build step.  Any manual modifications will be lost.")
            sb.AppendLine("Imports VBClasses")

            sb.AppendLine("Public Class algorithmList")

            sb.AppendLine(vbTab + "Public Function createAlgorithm(algorithmName as string) as taskParent")

            For Each className As String In vbClasses.Keys
                sb.AppendLine($"If algorithmName = ""{className}"" Then Return New {className}")
            Next
            sb.AppendLine(vbTab + vbTab + "Return Nothing")
            sb.AppendLine(vbTab + "End Function")
            sb.AppendLine("End Class")

            File.WriteAllText(algorithmListPath, sb.ToString())

            ' Extract first tokens from class names and write to GroupButtons.txt
            Dim groupTokens As New SortedList(Of String, String)
            For Each className As String In vbClasses.Keys
                Dim parts() As String = className.Split("_"c)
                If parts.Length > 0 AndAlso Not String.IsNullOrEmpty(parts(0)) Then
                    If Not groupTokens.ContainsKey(parts(0)) Then
                        groupTokens.Add(parts(0), parts(0))
                    End If
                End If
            Next

            Dim groupButtonsPath As String = Path.Combine(dataPath, "GroupButtonList.txt")
            Dim groupSb As New System.Text.StringBuilder()
            For Each token As String In groupTokens.Keys
                groupSb.AppendLine(token)
            Next
            File.WriteAllText(groupButtonsPath, groupSb.ToString())

            Dim AvailableAlgorithmsPath As String = Path.Combine(dataPath, "AvailableAlgorithms.txt")
            Dim algList As New System.Text.StringBuilder()
            For Each token As String In vbClasses.Keys
                algList.AppendLine(token)
            Next
            File.WriteAllText(AvailableAlgorithmsPath, algList.ToString())

            Console.WriteLine()
            Console.WriteLine()
            Console.WriteLine($"Found {vbClasses.Count} classes that inherit from TaskParent:")
            Console.WriteLine($"Found {xoClasses.Count} XO_ classes that inherit from TaskParent:")
            Console.WriteLine($"Total source lines read: {totalLinesRead}")
            Console.WriteLine($"Reference algorithm count: {xoClasses.Count}")
            Console.WriteLine($"Average lines per algorithm: " + (totalLinesRead \ (vbClasses.Count + xoClasses.Count)).ToString())
            Console.WriteLine($"Algorithm groups: {groupTokens.Count}")
            Console.WriteLine()
            Console.WriteLine()
        End Using
    End Sub

    Function ExtractClassName(line As String) As String
        ' Line format: "Public Class ClassName : Inherits TaskParent"
        Dim trimmedLine As String = line.Trim()
        If trimmedLine.StartsWith("'") Then Return Nothing

        ' Find "Class " and ": Inherits"
        Dim classIndex As Integer = trimmedLine.IndexOf("Class ")
        Dim inheritsIndex As Integer = trimmedLine.IndexOf(": Inherits")

        If classIndex >= 0 AndAlso inheritsIndex > classIndex Then
            Dim startPos As Integer = classIndex + 6  ' Length of "Class "
            Dim className As String = trimmedLine.Substring(startPos, inheritsIndex - startPos).Trim()
            Return className
        End If

        Return Nothing
    End Function
End Module
